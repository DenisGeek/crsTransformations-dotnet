buildscript {
    ext.kotlin_version = '1.2.71'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // https://mvnrepository.com/artifact/org.jetbrains.dokka/dokka-gradle-plugin
        // https://github.com/JetBrains/kotlin-examples/blob/master/gradle/dokka-gradle-example/build.gradle
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.16"
    }
}

plugins {
    id 'maven-publish' // https://docs.gradle.org/current/userguide/publishing_maven.html#publishing_maven
    // the above configuration enables the tasks "publish" and "publishToMavenLocal"
    // which you can verify by running "gradlew tasks" (i.e. run the command with and without the above 'maven-publish')
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'org.jetbrains.dokka'

    group = 'com.programmerare.crsTransformations'
    version = '1.0'

    sourceCompatibility = 1.8
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
        outputFormat = 'javadoc'
        outputDirectory = javadoc.destinationDir
    }

    task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    repositories {
        // GeoTools
        // http://docs.geotools.org/latest/userguide/tutorial/quickstart/maven.html
        // https://github.com/geotools/geotools/blob/master/pom.xml
        maven {
            url "https://repo.boundlessgeo.com/release/"
        }
        maven {
            url "http://download.osgeo.org/webdav/geotools/"
        }

        mavenCentral()
        jcenter()
        google()
    }

    dependencies {
        implementation("org.jetbrains.kotlin:kotlin-runtime:$kotlin_version")
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")
    }
    test {
        // Enable JUnit 5 (Gradle 4.6+).
        useJUnitPlatform()

        // Always run tests, even when nothing changed.
        // dependsOn 'cleanTest'

        testLogging {
            events "passed", "skipped", "failed" // , "standardOut", "standardError"
        }
    }

}

// ---------------------------------------------------------------------------
// Note that when any of the five (GeoTools , Goober , Orbis , Proj4j , NGA)
// third-part libraries below are upgraded then new test files
// should be generated with CoordinateTestDataGeneratedFromEpsgDatabaseTest
// (with the new version number in the file suffix) and compared with previous result
// (e.g. comparing the files manually with WinMerge until more automation has been implemented)
// ---------------------------------------------------------------------------
project(':crsTransformationGeoTools') {
    dependencies {
        implementation project(':crsTransformationCore')

        // https://mvnrepository.com/artifact/org.geotools/gt-main?repo=boundless
        implementation("org.geotools:gt-main:20.0") // NOTE that if you upgrade then use file CoordinateTestDataGeneratedFromEpsgDatabaseTest
        implementation("org.geotools:gt-epsg-hsql:20.0") // runtime exception if this is not included: org.opengis.referencing.NoSuchAuthorityCodeException: No code "EPSG:4326" from authority "EPSG" found for object of type "EngineeringCRS".
        implementation("javax.media:jai_core:1.1.3") // otherwise geotools causes this: Could not find jai_core.jar (javax.media:jai_core:1.1.3)
        // https://mvnrepository.com/artifact/org.geotools/gt-data?repo=boundless
        // https://mvnrepository.com/artifact/org.geotools/gt-shapefile?repo=boundless
        // https://mvnrepository.com/artifact/org.geotools/gt-main?repo=boundless
        // https://mvnrepository.com/artifact/org.geotools/gt-epsg-hsql?repo=boundless
        // http://geotools.org/
        // https://sourceforge.net/projects/geotools/
        // Used for shapefile parsing in the code generation module:
//        implementation group: 'org.geotools', name: 'gt-data', version: '20.0' // NOTE that if you upgrade then use file CoordinateTestDataGeneratedFromEpsgDatabaseTest
//        implementation group: 'org.geotools', name: 'gt-shapefile', version: '20.0'
    }
    publishing {
        publications {
            crsTransformationGeoTools(MavenPublication) {
                groupId = 'com.programmerare.crs-transformations'
                artifactId = 'crs-transformations-geotools'
                version = '0.9'
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

project(':crsTransformationGooberCTL') {
    dependencies {
        implementation project(':crsTransformationCore')

        // https://mvnrepository.com/artifact/com.github.goober/coordinate-transformation-library
        implementation("com.github.goober:coordinate-transformation-library:1.1") // NOTE that if you upgrade then use file CoordinateTestDataGeneratedFromEpsgDatabaseTest
    }
    publishing {
        publications {
            crsTransformationGooberCTL(MavenPublication) {
                groupId = 'com.programmerare.crs-transformations'
                artifactId = 'crs-transformations-goober'
                version = '0.9'
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

project(':crsTransformationOrbisgisCTS') {
    dependencies {
        implementation project(':crsTransformationCore')

        // https://mvnrepository.com/artifact/org.orbisgis/cts
        implementation("org.orbisgis:cts:1.5.1") // NOTE that if you upgrade then use file CoordinateTestDataGeneratedFromEpsgDatabaseTest
    }
    publishing {
        publications {
            crsTransformationOrbisgisCTS(MavenPublication) {
                groupId = 'com.programmerare.crs-transformations'
                artifactId = 'crs-transformations-orbisgis'
                version = '0.9'
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

project(':crsTransformationProj4J') {
    dependencies {
        implementation project(':crsTransformationCore')

        // https://mvnrepository.com/artifact/org.osgeo/proj4j
        implementation("org.osgeo:proj4j:0.1.0") // NOTE that if you upgrade then use file CoordinateTestDataGeneratedFromEpsgDatabaseTest
    }
    publishing {
        publications {
            crsTransformationProj4J(MavenPublication) {
                groupId = 'com.programmerare.crs-transformations'
                artifactId = 'crs-transformations-proj4j'
                version = '0.9'
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }    
}

project(':crsTransformationGeoPackageNGA') {
    dependencies {
        implementation project(':crsTransformationCore')

        // https://mvnrepository.com/artifact/mil.nga.geopackage/geopackage
        // https://github.com/ngageoint/GeoPackage
        implementation group: 'mil.nga.geopackage', name: 'geopackage', version: '3.1.0' // NOTE that if you upgrade then use file CoordinateTestDataGeneratedFromEpsgDatabaseTest
        implementation group: 'mil.nga.geopackage', name: 'geopackage-core', version: '3.1.0'
    }
    publishing {
        publications {
            crsTransformationGeoPackageNGA(MavenPublication) {
                groupId = 'com.programmerare.crs-transformations'
                artifactId = 'crs-transformations-nga'
                version = '0.9'
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

project(':crsTransformationTest') {
    dependencies {
        implementation project(':crsTransformationCore'), project(':crsTransformationOrbisgisCTS'), project(':crsTransformationGooberCTL'), project(':crsTransformationGeoTools'), project(':crsTransformationProj4J'), project(':crsTransformationGeoPackageNGA'), project(':crsConstants')
        // https://mvnrepository.com/artifact/org.geotools/gt-shapefile
//        implementation("group:org.geotools:gt-shapefile:19.1")
//        implementation("group:org.geotools:gt-data:19.1")
//        implementation("group:org.geotools:gt-shapefile:19.1")
// https://mvnrepository.com/artifact/org.geotools/gt-shapefile
//        implementation group: 'org.geotools', name: 'gt-shapefile', version: '19.1'
        // https://mvnrepository.com/artifact/org.mockito/mockito-core
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.23.0'

        // https://mvnrepository.com/artifact/org.junit.jupiter
        // https://github.com/junit-team/junit5/releases
        testImplementation("org.junit.jupiter:junit-jupiter-engine:5.3.1")
        testImplementation("org.junit.jupiter:junit-jupiter-params:5.3.1")

        testImplementation("org.hamcrest:hamcrest-library:1.3")

        // https://mvnrepository.com/artifact/com.google.guava/guava
        // https://github.com/google/guava/releases
        testImplementation group: 'com.google.guava', name: 'guava', version: '27.0-jre'
    }
}

project(':crsCodeGeneration') {
    // command line execution  examples:
    // gradle generateClassesWithEpsgConstants --args="v9_5_4 epsg_version_9_5_4 dbUserName dbUserPassword java"
    // gradle generateClassesWithEpsgConstants --args="v9_5_4 epsg_version_9_5_4 dbUserName dbUserPassword csv"
    task generateClassesWithEpsgConstants(type:JavaExec) {
        main = project.hasProperty("mainClass") ? project.getProperty("mainClass") : "com.programmerare.crsCodeGeneration.constantsGenerator.ConstantClassGenerator"
        classpath = sourceSets.main.runtimeClasspath
    }

    // command line execution
    // gradle generateCsvTestDataFromEpsgDatabaseAndShapefile
    // or, if something else than the default/hardcoded class name should be used:
    // gradle generateCsvTestDataFromEpsgDatabaseAndShapefile -PmainClass=com.programmerare.crsCodeGeneration.coordinateTestDataGenerator.CoordinateTestDataGenerator
    task generateCsvTestDataFromEpsgDatabaseAndShapefile(type:JavaExec) {
        main = project.hasProperty("mainClass") ? project.getProperty("mainClass") : "com.programmerare.crsCodeGeneration.coordinateTestDataGenerator.CoordinateTestDataGenerator"
        classpath = sourceSets.main.runtimeClasspath
    }

    dependencies {
        // https://mvnrepository.com/artifact/org.freemarker/freemarker
        implementation("org.freemarker:freemarker:2.3.28")

        // https://mvnrepository.com/artifact/org.mariadb.jdbc/mariadb-java-client
        implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.3.0'

        // https://mvnrepository.com/artifact/org.springframework/spring-jdbc
        // https://github.com/spring-projects/spring-framework/releases
        implementation("org.springframework:spring-jdbc:5.1.1.RELEASE")

        // https://mvnrepository.com/artifact/org.junit.jupiter
        // https://github.com/junit-team/junit5/releases
        testImplementation("org.junit.jupiter:junit-jupiter-engine:5.3.1")
        testImplementation("org.junit.jupiter:junit-jupiter-params:5.3.1")


        // https://mvnrepository.com/artifact/org.geotools/gt-main?repo=boundless
//        implementation("org.geotools:gt-main:20.0") // NOTE that if you upgrade then use file CoordinateTestDataGeneratedFromEpsgDatabaseTest
//        implementation("org.geotools:gt-epsg-hsql:20.0") // runtime exception if this is not included: org.opengis.referencing.NoSuchAuthorityCodeException: No code "EPSG:4326" from authority "EPSG" found for object of type "EngineeringCRS".
//        implementation("javax.media:jai_core:1.1.3") // otherwise geotools causes this: Could not find jai_core.jar (javax.media:jai_core:1.1.3)
        // https://mvnrepository.com/artifact/org.geotools/gt-data?repo=boundless
        // https://mvnrepository.com/artifact/org.geotools/gt-shapefile?repo=boundless
        // https://mvnrepository.com/artifact/org.geotools/gt-main?repo=boundless
        // https://mvnrepository.com/artifact/org.geotools/gt-epsg-hsql?repo=boundless
        // http://geotools.org/
        // https://sourceforge.net/projects/geotools/
        // Used for shapefile parsing in the code generation module:
        implementation group: 'org.geotools', name: 'gt-data', version: '20.0' // NOTE that if you upgrade then use file CoordinateTestDataGeneratedFromEpsgDatabaseTest
        implementation group: 'org.geotools', name: 'gt-shapefile', version: '20.0'        
    }
}

project(':crsTransformationCore') {
    publishing {
        publications {
            crsTransformationCore(MavenPublication) {
                groupId = 'com.programmerare.crs-transformations'
                artifactId = 'crs-transformations-core'
                version = '0.9'
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

project(':crsConstants') {
    publishing {
        publications {
            crsConstants(MavenPublication) {
                groupId = 'com.programmerare.crs-transformations'
                artifactId = 'crs-transformations-constants'
                version = '0.9'
                from components.java
                artifact sourcesJar
                // artifact javadocJar // should be enabled but currently disabled since it takes a very long time
            }
        }
    }
}

// https://stackoverflow.com/questions/35143514/publishing-of-a-modular-library-to-maven-using-gradle
//subprojects{
//    publishing {
//        publications {
//            "$project.name"(MavenPublication) {
//                groupId project.group
//                artifactId project.name
//                version project.version
//                from components.java
//            }
//        }
//    }
//}