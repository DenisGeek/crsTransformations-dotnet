buildscript {
    ext.kotlin_version = '1.2.71'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // https://mvnrepository.com/artifact/org.jetbrains.dokka/dokka-gradle-plugin
        // https://github.com/JetBrains/kotlin-examples/blob/master/gradle/dokka-gradle-example/build.gradle
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.16"
    }
}

plugins {
    id 'maven-publish' // https://docs.gradle.org/current/userguide/publishing_maven.html#publishing_maven
    // the above configuration enables the tasks "publish" and "publishToMavenLocal"
    // which you can verify by running "gradlew tasks" (i.e. run the command with and without the above 'maven-publish')
}

ext.crsTransformationVersion = '0.9'

allprojects {
    repositories {
        maven {
            url "https://download.osgeo.org/webdav/geotools/"
            // https://github.com/akhikhl/gretty/issues/322			
            // https://download.osgeo.org/webdav/geotools/org/geotools/gt-main/20.0/
            // https://download.osgeo.org/webdav/geotools/javax/media/jai_core/1.1.3/
        }
        maven {
            url "http://maven.geotoolkit.org/"
        }
        maven {
            url "https://repo.boundlessgeo.com/release/"
        }
        mavenCentral()
        jcenter()
        google()
    }
	

}
		
subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'org.jetbrains.dokka'

    group = 'com.programmerare.crs-transformation'
    version = rootProject.crsTransformationVersion

    sourceCompatibility = 1.8
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
        kotlinOptions.javaParameters = true //Will retain parameter names e.g. useful for intellisense parameter information instead of "arg0" and "arg1" and so on
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
        outputFormat = 'javadoc'
        outputDirectory = javadoc.destinationDir
    }

    task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    dependencies {
        implementation("org.jetbrains.kotlin:kotlin-runtime:$kotlin_version")
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")
    }
    test {
        // Enable JUnit 5 (Gradle 4.6+).
        useJUnitPlatform()

        // Always run tests, even when nothing changed.
        // dependsOn 'cleanTest'

        testLogging {
            events "passed", "skipped", "failed" // , "standardOut", "standardError"
        }
    }

}

// ---------------------------------------------------------------------------
// Note that when any of the five (GeoTools , Goober , Orbis , Proj4j , NGA)
// third-part libraries below are upgraded then new test files
// should be generated with CoordinateTestDataGeneratedFromEpsgDatabaseTest
// (with the new version number in the file suffix) and compared with previous result
// (e.g. comparing the files manually with WinMerge until more automation has been implemented)
// ---------------------------------------------------------------------------
project(':crs-transformation-adapter-impl-geotools') {
    dependencies {
        implementation project(':crs-transformation-adapter-core')

        // https://mvnrepository.com/artifact/org.geotools/gt-main?repo=boundless
        implementation("org.geotools:gt-main:20.0") // NOTE that if you upgrade then use file CoordinateTestDataGeneratedFromEpsgDatabaseTest
        implementation("org.geotools:gt-epsg-hsql:20.0") // runtime exception if this is not included: org.opengis.referencing.NoSuchAuthorityCodeException: No code "EPSG:4326" from authority "EPSG" found for object of type "EngineeringCRS".
        // test code will detect upgrades with the purpose to update an enum if the file name changes from "gt-api-20.0.jar"

        implementation("javax.media:jai_core:1.1.3") // otherwise geotools causes this: Could not find jai_core.jar (javax.media:jai_core:1.1.3)

        // https://mvnrepository.com/artifact/org.geotools/gt-data?repo=boundless
        // https://mvnrepository.com/artifact/org.geotools/gt-shapefile?repo=boundless
        // https://mvnrepository.com/artifact/org.geotools/gt-main?repo=boundless
        // https://mvnrepository.com/artifact/org.geotools/gt-epsg-hsql?repo=boundless
        // http://geotools.org/
        // https://sourceforge.net/projects/geotools/
        // Used for shapefile parsing in the code generation module:
//        implementation group: 'org.geotools', name: 'gt-data', version: '20.0' // NOTE that if you upgrade then use file CoordinateTestDataGeneratedFromEpsgDatabaseTest
//        implementation group: 'org.geotools', name: 'gt-shapefile', version: '20.0'
    }
    publishing {
        publications {
            crstransformationsgeotools(MavenPublication) {
                groupId = 'com.programmerare.crs-transformation'
                artifactId = 'crs-transformation-adapter-impl-geotools'
                version = rootProject.crsTransformationVersion
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

project(':crs-transformation-adapter-impl-goober') {
    dependencies {
        implementation project(':crs-transformation-adapter-core')

        // https://mvnrepository.com/artifact/com.github.goober/coordinate-transformation-library
        implementation("com.github.goober:coordinate-transformation-library:1.1") // NOTE that if you upgrade then use file CoordinateTestDataGeneratedFromEpsgDatabaseTest
        // test code will detect upgrades with the purpose to update an enum if the file name changes from "coordinate-transformation-library-1.1.jar"
    }
    publishing {
        publications {
            crstransformationsgoober(MavenPublication) {
                groupId = 'com.programmerare.crs-transformation'
                artifactId = 'crs-transformation-adapter-impl-goober'
                version = rootProject.crsTransformationVersion
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

project(':crs-transformation-adapter-impl-orbisgis') {
    dependencies {
        implementation project(':crs-transformation-adapter-core')

        // https://mvnrepository.com/artifact/org.orbisgis/cts
        implementation("org.orbisgis:cts:1.5.1") // NOTE that if you upgrade then use file CoordinateTestDataGeneratedFromEpsgDatabaseTest
        // test code will detect upgrades with the purpose to update an enum if the file name changes from "cts-1.5.1.jar"
    }
    publishing {
        publications {
            crstransformationsorbisgis(MavenPublication) {
                groupId = 'com.programmerare.crs-transformation'
                artifactId = 'crs-transformation-adapter-impl-orbisgis'
                version = rootProject.crsTransformationVersion
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

project(':crs-transformation-adapter-impl-proj4j') {
    dependencies {
        implementation project(':crs-transformation-adapter-core')

        // https://mvnrepository.com/artifact/org.osgeo/proj4j
        implementation("org.osgeo:proj4j:0.1.0") // NOTE that if you upgrade then use file CoordinateTestDataGeneratedFromEpsgDatabaseTest
        // test code will detect upgrades with the purpose to update an enum if the file name changes from "proj4j-0.1.0.jar"
    }
    publishing {
        publications {
            crstransformationsproj4j(MavenPublication) {
                groupId = 'com.programmerare.crs-transformation'
                artifactId = 'crs-transformation-adapter-impl-proj4j'
                version = rootProject.crsTransformationVersion
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }    
}

project(':crs-transformation-adapter-impl-nga') {
    dependencies {
        implementation project(':crs-transformation-adapter-core')

        // https://mvnrepository.com/artifact/mil.nga.geopackage/geopackage
        // https://github.com/ngageoint/GeoPackage
        implementation group: 'mil.nga.geopackage', name: 'geopackage', version: '3.1.0' // NOTE that if you upgrade then use file CoordinateTestDataGeneratedFromEpsgDatabaseTest
        implementation group: 'mil.nga.geopackage', name: 'geopackage-core', version: '3.1.0'
        // test code will detect upgrades with the purpose to update an enum if the file name changes from "geopackage-core-3.1.0.jar"
    }
    publishing {
        publications {
            crstransformationsnga(MavenPublication) {
                groupId = 'com.programmerare.crs-transformation'
                artifactId = 'crs-transformation-adapter-impl-nga'
                version = rootProject.crsTransformationVersion
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

project(':crs-transformation-adapter-test') {
    dependencies {
        implementation project(':crs-transformation-adapter-core'), project(':crs-transformation-adapter-impl-orbisgis'), project(':crs-transformation-adapter-impl-goober'), project(':crs-transformation-adapter-impl-geotools'), project(':crs-transformation-adapter-impl-proj4j'), project(':crs-transformation-adapter-impl-nga'), project(':crs-transformation-constants')
        // https://mvnrepository.com/artifact/org.geotools/gt-shapefile
//        implementation("group:org.geotools:gt-shapefile:19.1")
//        implementation("group:org.geotools:gt-data:19.1")
//        implementation("group:org.geotools:gt-shapefile:19.1")
// https://mvnrepository.com/artifact/org.geotools/gt-shapefile
//        implementation group: 'org.geotools', name: 'gt-shapefile', version: '19.1'
        // https://mvnrepository.com/artifact/org.mockito/mockito-core
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.23.0'

        // https://mvnrepository.com/artifact/org.junit.jupiter
        // https://github.com/junit-team/junit5/releases
        testImplementation("org.junit.jupiter:junit-jupiter-engine:5.3.1")
        testImplementation("org.junit.jupiter:junit-jupiter-params:5.3.1")

        testImplementation("org.hamcrest:hamcrest-library:1.3")

        // https://mvnrepository.com/artifact/com.google.guava/guava
        // https://github.com/google/guava/releases
        testImplementation group: 'com.google.guava', name: 'guava', version: '27.0-jre'
    }
}

project(':crs-transformation-code-generation') {
    // command line execution  examples:
    // gradle generateClassesWithEpsgConstants --args="v9_5_4 epsg_version_9_5_4 dbUserName dbUserPassword java"
    // gradle generateClassesWithEpsgConstants --args="v9_5_4 epsg_version_9_5_4 dbUserName dbUserPassword csv"
    task generateClassesWithEpsgConstants(type:JavaExec) {
        main = project.hasProperty("mainClass") ? project.getProperty("mainClass") : "com.programmerare.crs-transformation-adapter-impl-code-generation.constantsGenerator.ConstantClassGenerator"
        classpath = sourceSets.main.runtimeClasspath
    }

    // command line execution
    // gradle generateCsvTestDataFromEpsgDatabaseAndShapefile
    // or, if something else than the default/hardcoded class name should be used:
    // gradle generateCsvTestDataFromEpsgDatabaseAndShapefile -PmainClass=com.programmerare.crs-transformation-adapter-impl-code-generation.coordinateTestDataGenerator.CoordinateTestDataGenerator
    task generateCsvTestDataFromEpsgDatabaseAndShapefile(type:JavaExec) {
        main = project.hasProperty("mainClass") ? project.getProperty("mainClass") : "com.programmerare.crs-transformation-adapter-impl-code-generation.coordinateTestDataGenerator.CoordinateTestDataGenerator"
        classpath = sourceSets.main.runtimeClasspath
    }

    dependencies {
        // https://mvnrepository.com/artifact/org.freemarker/freemarker
        implementation("org.freemarker:freemarker:2.3.28")

        // https://mvnrepository.com/artifact/org.mariadb.jdbc/mariadb-java-client
        implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.3.0'

        // https://mvnrepository.com/artifact/org.springframework/spring-jdbc
        // https://github.com/spring-projects/spring-framework/releases
        implementation("org.springframework:spring-jdbc:5.1.1.RELEASE")

        // https://mvnrepository.com/artifact/org.junit.jupiter
        // https://github.com/junit-team/junit5/releases
        testImplementation("org.junit.jupiter:junit-jupiter-engine:5.3.1")
        testImplementation("org.junit.jupiter:junit-jupiter-params:5.3.1")


        // https://mvnrepository.com/artifact/org.geotools/gt-main?repo=boundless
//        implementation("org.geotools:gt-main:20.0") // NOTE that if you upgrade then use file CoordinateTestDataGeneratedFromEpsgDatabaseTest
//        implementation("org.geotools:gt-epsg-hsql:20.0") // runtime exception if this is not included: org.opengis.referencing.NoSuchAuthorityCodeException: No code "EPSG:4326" from authority "EPSG" found for object of type "EngineeringCRS".
//        implementation("javax.media:jai_core:1.1.3") // otherwise geotools causes this: Could not find jai_core.jar (javax.media:jai_core:1.1.3)
        // https://mvnrepository.com/artifact/org.geotools/gt-data?repo=boundless
        // https://mvnrepository.com/artifact/org.geotools/gt-shapefile?repo=boundless
        // https://mvnrepository.com/artifact/org.geotools/gt-main?repo=boundless
        // https://mvnrepository.com/artifact/org.geotools/gt-epsg-hsql?repo=boundless
        // http://geotools.org/
        // https://sourceforge.net/projects/geotools/
        // Used for shapefile parsing in the code generation module:
        implementation group: 'org.geotools', name: 'gt-data', version: '20.0' // NOTE that if you upgrade then use file CoordinateTestDataGeneratedFromEpsgDatabaseTest
        implementation group: 'org.geotools', name: 'gt-shapefile', version: '20.0'        
    }
}

project(':crs-transformation-adapter-core') {
    publishing {
        publications {
            crstransformationscore(MavenPublication) {
                groupId = 'com.programmerare.crs-transformation'
                artifactId = 'crs-transformation-adapter-core'
                version = rootProject.crsTransformationVersion
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
    dependencies {
        // https://mvnrepository.com/artifact/org.junit.jupiter
        // https://github.com/junit-team/junit5/releases
        testImplementation("org.junit.jupiter:junit-jupiter-engine:5.3.1")
        testImplementation("org.junit.jupiter:junit-jupiter-params:5.3.1")
    }
}

project(':crs-transformation-constants') {
    publishing {
        publications {
            crstransformationsconstants(MavenPublication) {
                groupId = 'com.programmerare.crs-transformation'
                artifactId = 'crs-transformation-constants'
                version = rootProject.crsTransformationVersion
                from components.java
                artifact sourcesJar
                // artifact javadocJar // should be enabled but currently disabled since it takes a very long time
            }
        }
    }
}


		
// https://stackoverflow.com/questions/35143514/publishing-of-a-modular-library-to-maven-using-gradle
//subprojects{
//    publishing {
//        publications {
//            "$project.name"(MavenPublication) {
//                groupId project.group
//                artifactId project.name
//                version project.version
//                from components.java
//            }
//        }
//    }
//}