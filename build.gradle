buildscript {
	ext.kotlin_version = '1.2.41'

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'kotlin'

	group = 'com.programmerare.crsTransformations'
	version = '1.0'

	repositories {
		// GeoTools
		// http://docs.geotools.org/latest/userguide/tutorial/quickstart/maven.html
		// https://github.com/geotools/geotools/blob/master/pom.xml
		maven {
			url "https://repo.boundlessgeo.com/release/"
		}
		maven {
			url "http://download.osgeo.org/webdav/geotools/"
		}

		mavenCentral()
		jcenter()
		google()
	}

	dependencies {
		implementation("org.jetbrains.kotlin:kotlin-runtime:$kotlin_version")
		implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")
		testImplementation("org.junit.jupiter:junit-jupiter-engine:5.2.0")
		testImplementation("org.junit.jupiter:junit-jupiter-params:5.2.0")
		testImplementation("org.hamcrest:hamcrest-library:1.3")


		// TODO: not all modules will need the dependencies below
		implementation("com.github.goober:coordinate-transformation-library:1.1")
		implementation("org.orbisgis:cts:1.5.1")
		implementation("org.osgeo:proj4j:0.1.0")
		implementation("org.geotools:gt-main:19.1")
		implementation("org.geotools:gt-epsg-hsql:19.1") // runtime exception if this is not included: org.opengis.referencing.NoSuchAuthorityCodeException: No code "EPSG:4326" from authority "EPSG" found for object of type "EngineeringCRS".
		implementation("javax.media:jai_core:1.1.3") // otherwise geotools causes this: Could not find jai_core.jar (javax.media:jai_core:1.1.3)
		// implementation("org.geotools:gt-epsg-hsql:19.1")
	}
}

project(':crsTransformationGeoTools') {
	dependencies {
		implementation project(':crsTransformationCore')
	}
}

project(':crsTransformationGooberCTL') {
    dependencies {
        implementation project(':crsTransformationCore')
    }
}

project(':crsTransformationOrbisgisCTS') {
    dependencies {
        implementation project(':crsTransformationCore')
    }
}

project(':crsTransformationProj4J') {
	dependencies {
		implementation project(':crsTransformationCore')
	}
}

project(':crsTransformationTest') {
	dependencies {
		implementation project(':crsTransformationCore'), project(':crsTransformationOrbisgisCTS'), project(':crsTransformationGooberCTL'), project(':crsTransformationGeoTools'), project(':crsTransformationProj4J'), project(':crsConstants')
	}
}

project(':crsCodeGeneration') {
	// command line execution
	// gradle execute
	// or, if somethign else than the default/hardcoded class name should be used:
	// gradle execute -PmainClass=com.programmerare.crsCodeGeneration.ConstantClassGenerator
	task execute(type:JavaExec) {
		main = project.hasProperty("mainClass") ? project.getProperty("mainClass") : "com.programmerare.crsCodeGeneration.ConstantClassGenerator"
		classpath = sourceSets.main.runtimeClasspath
	}

	dependencies {
		// https://mvnrepository.com/artifact/org.freemarker/freemarker
		implementation("org.freemarker:freemarker:2.3.28")
		// https://mvnrepository.com/artifact/net.sf.ucanaccess/ucanaccess
		implementation("net.sf.ucanaccess:ucanaccess:4.0.4")
		// https://mvnrepository.com/artifact/org.springframework/spring-jdbc
		implementation("org.springframework:spring-jdbc:5.0.6.RELEASE")
	}
}
